number = /[0-9]+/;

sides =	number;
diceCount = number;

opRoll = /d/;
opMul = '*';
opDiv = '/';
opAdd = '+';
opSub	= '-';
opVersus = 'vs';

string = /\w+/;
(* quotedString = '"' [^"]* '"'; *)
charName = nonNumericString;
spacedNotVsString = string {!opVersus string}*;
ability = spacedNotVsString;


nonNumericString = !number string;

abilityLookup = charName ability;


diceRoll = diceCount opRoll sides
         | opRoll sides
         ;

atom = diceRoll
     | '(' expr ')'
     | number
     ;

operator = opMul
         | opDiv
         | opAdd
         | opSub
         ;


product = atom {(opMul | opDiv) atom}*;
sum = product {(opAdd | opSub) product}*;

expr = sum;

versusAbility = opVersus abilityLookup {operator expr}*;

versusExpr = opVersus [nonNumericString] expr
           | versusAbility
           ;

rollExpr = expr [versusExpr];
rollAbility = abilityLookup {operator expr}* [versusExpr];

roll = rollExpr
     | rollAbility;



modifier = number
         | opAdd number
         | opSub number
         ;

(* charName attackName attackModifier damageModifier ac *)
(* attackRoll = string {string}+ [modifier] [modifier] number; *)


