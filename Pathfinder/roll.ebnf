number = /[0-9]+/;

sides =	number;
diceCount = number;

opRoll = /d/;
opMul = '*';
opDiv = '/';
opAdd = '+';
opSub	= '-';
opVersus = 'vs';

string = /\w+/;

quotedString = /"/ /[^"]*/ /"/;

charName = !opVersus nonNumericString;
spacedNotVsString = string {!opVersus !number string}*;
ability = spacedNotVsString
        | quotedString
        ;


nonNumericString = !number string;

abilityLookup = charName ability;


diceRoll = diceCount opRoll sides
         | opRoll sides
         ;

atom = number !opRoll
     | '(' expr ')'
     | diceRoll
     | abilityLookup
     ;

operator = opMul
         | opDiv
         | opAdd
         | opSub
         ;

product = atom {(opMul | opDiv) atom}*;
sum = product {(opAdd | opSub) product}*;

expr = sum;


rollExpr = expr [opVersus expr];
rollAbility = abilityLookup {operator expr}* [opVersus expr];

roll = rollAbility
     | rollExpr
     ;

modifier = number
         | opAdd number
         | opSub number
         ;


sign = '+' | '-';

bonus = sign {expr}+
      | {expr}+
      ;

attackLookup = charName ability;
modifiers = [bonus] [',' bonus];
vsExpr = opVersus expr;
attackRoll = attackLookup [modifiers] [vsExpr];
